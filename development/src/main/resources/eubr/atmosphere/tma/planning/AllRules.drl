package eubr.atmosphere.tma.planning

dialect "mvel"

import eubr.atmosphere.tma.planning.AdaptationManager
import eubr.atmosphere.tma.data.Action
import eubr.atmosphere.tma.data.Configuration
import eubr.atmosphere.tma.utils.TrustworthinessScore

declare TrustworthinessScore
      @role(event)
      @expires(1s)
      //@timestamp (eventDestinationTimestamp)
 end

rule "Starling Actuation Rule"
    when
        $score: TrustworthinessScore ( securityScore.score > 0.1400 )
    then
        System.out.println("Starling Actuation");
        Action action = new Action(80001, "createCase", $score.securityScore.resourceId, 80001);
        action.addConfiguration(new Configuration(80001, "incidentId", "38"));
        action.addConfiguration(new Configuration(80002, "incidentSource", "CloudEA"));
        action.addConfiguration(new Configuration(80003, "subject", "Non-complient policy discovered"));
        action.addConfiguration(new Configuration(80004, "summary",
            "Non-complient policy discovered by Boris Giterman. Please check your McAffee policy distribution"));
        action.addConfiguration(new Configuration(80005, "is_public", "true"));
        action.addConfiguration(new Configuration(80006, "caseType", "INCIDENT"));
        action.addConfiguration(new Configuration(80007, "aggregatedDomain", "Starling"));
        AdaptationManager.performAdaptation( action, AdaptationManager.obtainMetricData($score.securityScore) );
end


rule "Privacy Score Validation - Increase PRIVAAS Anonymization"
    when
        $score: TrustworthinessScore ( privacyScore.score < privacyScore.threshold )
    then
        Action action = new Action(35001, "update", 8, 35001);
        AdaptationManager.performAdaptation( action, AdaptationManager.obtainMetricData($score.privacyScore) );
end

rule "Privacy Score Validation - Not Increase PRIVAAS Anonymization"
    when
        $score: TrustworthinessScore ( privacyScore.score >= privacyScore.threshold,
                                       privacyScore.score != 0.0,
                                       privacyScore.threshold != 0.0)
    then
        Action action = new Action(35002, "none", 8, 35001);
        AdaptationManager.performAdaptation( action, AdaptationManager.obtainMetricData($score.privacyScore) );
end

